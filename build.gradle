buildscript {
	dependencies {
		classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta1'
		classpath 'commons-httpclient:commons-httpclient:3.1'
	}
}

plugins {
  id 'org.asciidoctor.convert' version '1.5.2'
	id 'com.bluepapa32.watch' version '0.1.5'
	id 'org.kordamp.gradle.livereload' version '0.2.1'
  id 'com.github.jruby-gradle.base' version '0.1.5'
}

apply plugin: 'com.bluepapa32.watch'
apply plugin: 'org.kordamp.gradle.livereload'
apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'

version = '1.0.0-SNAPSHOT'

ext {
	revealjsVersion = '2.6.2'
	asciidoctorBackendVersion = 'master'
	downloadDir = new File(buildDir,'download')
	templateDir = new File(downloadDir,'templates')
	revealjsDir   = new File(downloadDir,'reveal.js')
}

repositories {
    jcenter()
}

dependencies {
    gems 'rubygems:slim:2.1.0'
    gems 'rubygems:thread_safe:0.3.4'
}

task download << {
	mkdir downloadDir
	vfs {
		cp "zip:${project.projectDir}/lib/asciidoctor-reveal.js-${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
				templateDir, recursive:true, overwrite:true
		cp "zip:${project.projectDir}/lib/reveal.js-${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                revealjsDir, recursive:true, overwrite:true
	}
}

download {
	description "Download extra revealjs resources"
	outputs.dir templateDir
	outputs.dir revealjsDir
}

asciidoctor {

    dependsOn jrubyPrepareGems

	sources {
		include 'tdd-and-test-automation.adoc'
	}

	resources {
		from (sourceDir) {
			include 'images/**'
		}
		from (downloadDir) {
			include 'reveal.js/**'
		}
	}

	backends    'revealjs'

	attributes	'build-gradle': file('build.gradle'),
				'sourcedir': project.sourceSets.main.java.srcDirs[0],
				'endpoint-url': 'http://example.org',
				'source-highlighter' : 'coderay',
				'imagesdir':'./images',
				'toc':'left',
				'icons': 'font',
				'setanchors':'true',
				'idprefix':'',
				'idseparator':'-',
				'docinfo1':'true',
				'revealjs_transition':'linear',
				'revealjs_history':'true',
				'revealjs_slideNumber':'true'

	options template_dirs : [new File(templateDir,'templates/slim').absolutePath ]

	dependsOn download
}

watch {
  asciidoc {
	files fileTree('src')
	tasks 'asciidoctor'
  }
}

liveReload {
  docRoot asciidoctor.outputDir.canonicalPath
}

wrapper {
  gradleVersion = '2.5'
}
